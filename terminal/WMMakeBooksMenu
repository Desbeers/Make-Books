#!/bin/zsh

#####################################################
#                                                   #
# MAKE BOOKS menu for Window Maker                  #
#                                                   #
#####################################################

# Include the shared script:
. "${0:a:h:h}/includes/shared.zsh"
. $local/includes/variables.zsh

process() {
    cd ${book:a:h:h}
    # Double check if it actualy a book or just a file with the same name
    if [ -f ./assets/make-book.md ]; then
        metadata_file='assets/make-book.md'
        # Setup the variables:
        clear_variables
        . $local/includes/variables.zsh
        book_list["$author","$title"]=$PWD
    fi
}
#~/GNUstep/Defaults/WMMakeBooksMenuTEST

echo "Update Make Books menu for WindowMaker."

menu=$HOME/GNUstep/Defaults/WMMakeBooksMenu
# Open the menu
print "\"Make books\" MENU" > $menu
# Options
print "\"Options\" MENU" >> $menu
print "\"Refresh menu\"" "EXEC gnome-terminal --title='Update Make Books menu' --geometry=80x10 -- WMMakeBooksMenu" >> $menu
print "\"Options\" END" >> $menu
# Make an array
declare -A book_list
# Find all items
find  $books -name make-book.md -type f | while read book; do process "$book"; done
# Write the items
for key ("${(@ko)book_list}") {
    item=("${(@s/,/)key}")
    if [[ $sub != $item[1] ]] {
        if [[ $sub ]] {
            print $sub "END" >> $menu
        }
        print $item[1] "MENU" >> $menu
        sub=$item[1]
    }
    print $item[2] "MENU" >> $menu
    print "\"Make a PDF\"" "EXEC gnome-terminal --title='Make Book' --geometry=80x10 --working-directory='"$book_list[$key]"' -- make-book pdf" >> $menu
    print "\"Make an ePub\"" "EXEC gnome-terminal --title='Make Book' --geometry=80x10 --working-directory='"$book_list[$key]"' -- make-book epub" >> $menu
    print "\"Clean the export\"" "EXEC gnome-terminal --title='Make Book' --geometry=80x10 --working-directory='"$book_list[$key]"' -- make-book clean" >> $menu
    print $item[2] "END" >> $menu
}
# Close the last author
print $sub "END" >> $menu
# Close the menu
print "\"Make books\" END" >> $menu

